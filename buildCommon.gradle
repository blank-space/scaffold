apply {
    plugin "kotlin-android"
    plugin 'kotlin-kapt'
}

kapt {
    arguments {
        arg("AROUTER_MODULE_NAME", project.getName())
    }
}


android {
    compileSdkVersion Config.compileSdkVersion
    resourcePrefix suffix

    defaultConfig {
        if (project.name == "app") {
            applicationId Config.applicationId
            multiDexEnabled true
        }

        minSdkVersion Config.minSdkVersion
        versionCode Config.versionCode
        versionName Config.versionName
        consumerProguardFiles 'proguard-rules.pro'

        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath = true
            }
        }
    }

    buildTypes {
        debug {
            buildConfigField "boolean", "LOG_DEBUG", "true"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            buildConfigField "boolean", "LOG_DEBUG", "false"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding = true
        dataBinding = true
    }

    packagingOptions {
        exclude 'META-INF/library_release.kotlin_module'
        exclude "**/module-info.class"
    }

    sourceSets {
        main.jniLibs.srcDirs = ['libs']
    }


}

/**
 * 获取前缀
 * @return
 */
def getSuffix() {
    return project.name + "_"
}


