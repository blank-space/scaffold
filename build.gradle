// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ConfigUtils.init(gradle)
    repositories {
        // use for debug plugin local
        if (Config.depConfig.plugin_bus.useLocal || Config.depConfig.plugin_api.useLocal) {
            maven() { url uri("${project.rootDir.path}/mavenLocal") }
        }
        google()
        jcenter()
    }


    dependencies {
        classpath 'com.google.dagger:hilt-android-gradle-plugin:2.28.1-alpha'
        for (def entrySet : ConfigUtils.getApplyPlugins().entrySet()) {
            classpath entrySet.value.dep
        }
    }


}

allprojects {
    repositories {
        maven() { url uri("${project.rootDir.path}/mavenLocal") }
        maven { url "https://jitpack.io" }
        google()
        jcenter()
        maven {
            url 'https://maven.aliyun.com/repository/public'
        }
    }

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
        resolutionStrategy.eachDependency {
            if (it.requested.group == 'com.android.support'
                    && !it.requested.name.contains('multidex')) {
                it.useVersion Config.supportVersion
            }
        }
    }
}

subprojects {
    configurations.all {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->


            if (details.requested.group == 'androidx.fragment') {
                details.useVersion "1.3.5"
            }
            if (details.requested.group == 'androidx.core' && details.requested.name=='core') {
                details.useVersion "1.3.2"
            }

            if (details.requested.group == 'androidx.lifecycle' && details.requested.name =='lifecycle-livedata') {
                details.useVersion "2.2.0"
            }
            if (details.requested.group == 'androidx.appcompat') {
                details.useVersion "1.2.0"
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}